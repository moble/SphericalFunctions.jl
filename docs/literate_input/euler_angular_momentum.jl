md"""
# ``L_j`` and ``R_j`` with Euler angles

This package defines the angular-momentum operators ``L_j`` and ``R_j`` in terms of elements
of the Lie group and algebra:
```math
L_ğ® f(ğ‘) = \left. i \frac{d}{d\epsilon}\right|_{\epsilon=0}
f\left(e^{-\epsilon ğ®/2}\, ğ‘\right)
\qquad \text{and} \qquad
R_ğ® f(ğ‘) = -\left. i \frac{d}{d\epsilon}\right|_{\epsilon=0}
f\left(ğ‘\, e^{-\epsilon ğ®/2}\right),
```
This is certainly the natural realm for these operators, but it is not the common one.  In
particular, virtually all textbooks and papers on the subject define these operators in
terms of the standard spherical coordinates on the 2-sphere, rather than quaternions or even
Euler angles.  In particular, the standard forms are essentially always given in terms of
the Cartesian basis, as in ``L_x``, ``L_y``, and ``L_z`` â€” though some times the first two
are expressed as ``L_{\pm} = L_x \pm i L_y``.

Here, we will use SymPy to just grind through the algebra of expressing the angular-momentum
operators in terms of Euler angles, including evaluating the commutators in that form, and
further reduce them to operators in terms of spherical coordinates.  We will find a couple
important results that help make contact with more standard expressions:

  1. Our results for ``L_x``, ``L_x``, and ``L_z`` in spherical coordinates agree with
     standard expressions.
  2. The commutators obey ``[L_x, L_y] = i L_z`` and cyclic permutations, in agreement with
     the standard expressions.
  3. We also find ``[R_x, R_y] = i R_z`` and cyclic permutations.
  4. We can explicitly compute ``[L_i, R_j] = 0``, as expected.
  5. Using the natural extension of Goldberg et al.'s SWSHs to include ``\gamma``, we can
     see that the natural spin-weight operator is ``R_z = i \partial_\gamma``.  Thus, we
     define ``R_z = s`` for a function with spin weight ``s``.
  6. The spin-raising operator for ``R_z`` is ``\eth = R_x + i R_y``; the spin-lowering
     operator is ``\bar{\eth} = R_x - i R_y``.

## Analytical groundwork

We start by defining a new set of Euler angles according to
```math
\mathbf{R}_{\alpha', \beta', \gamma'}
= e^{-\theta \mathbf{u} / 2} \mathbf{R}_{\alpha, \beta, \gamma}
\qquad \text{or} \qquad
\mathbf{R}_{\alpha', \beta', \gamma'}
= \mathbf{R}_{\alpha, \beta, \gamma} e^{-\theta \mathbf{u} / 2}
```
where ``\mathbf{u}`` will be each of the basis quaternions, and each of ``\alpha'``,
``\beta'``, and ``\gamma'`` is a function of ``\alpha``, ``\beta``, ``\gamma``, and
``\theta``.  Then, we note that the chain rule tells us that
```math
\frac{\partial}{\partial \theta}
=
\frac{\partial \alpha'}{\partial \theta} \frac{\partial}{\partial \alpha'}
+ \frac{\partial \beta'}{\partial \theta} \frac{\partial}{\partial \beta'}
+ \frac{\partial \gamma'}{\partial \theta} \frac{\partial}{\partial \gamma'},
```
which we will use to convert the general expression for the angular-momentum operators in
terms of ``\partial_\theta`` into an expression in terms of derivatives with respect to
these new Euler angles:
```math
\begin{align}
  L_j f(\mathbf{R}_{\alpha, \beta, \gamma})
  &=
  \left. i \frac{\partial} {\partial \theta} f \left( e^{-\theta \mathbf{e}_j / 2}
  \mathbf{R}_{\alpha, \beta, \gamma} \right) \right|_{\theta=0}
  \\
  &=
  i \left[ \left(
      \frac{\partial \alpha'}{\partial \theta} \frac{\partial}{\partial \alpha'}
      + \frac{\partial \beta'}{\partial \theta} \frac{\partial}{\partial \beta'}
      + \frac{\partial \gamma'}{\partial \theta} \frac{\partial}{\partial \gamma'}
  \right) f \left(\alpha', \beta', \gamma'\right) \right]_{\theta=0}
  \\
  &=
  i \left[ \left(
      \frac{\partial \alpha'}{\partial \theta} \frac{\partial}{\partial \alpha}
      + \frac{\partial \beta'}{\partial \theta} \frac{\partial}{\partial \beta}
      + \frac{\partial \gamma'}{\partial \theta} \frac{\partial}{\partial \gamma}
  \right) f \left(\alpha, \beta, \gamma\right) \right]_{\theta=0},
\end{align}
```
and similarly for ``R_j``.

So the objective is to find the new Euler angles, differentiate with respect to ``\theta``,
and then evaluate at ``\theta = 0``.  We do this by first multiplying ``\mathbf{R}_{\alpha,
\beta, \gamma}`` and ``e^{-\theta \mathbf{u} / 2}`` in the desired order, then expanding the
results in terms of its quaternion components, and then computing the new Euler angles in
terms of those components according to the usual expression.

"""

#src # Do this first just to hide stdout of the conda installation step
# ````@setup euler_angular_momentum
# import SymPyPythonCall
# ````

# ## Computational infrastructure
# We'll use SymPy (via Julia) since `Symbolics.jl` isn't very good at trig yet.
import LaTeXStrings: @L_str, LaTeXString
import Quaternionic: Quaternionic, Quaternion, components
import SymPyPythonCall
import SymPyPythonCall: sympy, symbols, sqrt, sin, cos, tan, acos, atan, latex
const expand_trig = sympy.expand_trig
const Derivative = sympy.Derivative
const Ï€ = sympy.pi
const I = sympy.I
nothing  #hide

# Define coordinates we will use
Î±, Î², Î³, Î¸, Ï• = symbols("Î± Î² Î³ Î¸ Ï•", real=true, positive=true)
nothing  #hide

# Reinterpret the quaternion basis elements for compatibility with SymPy.  (`Quaternionic`
# defines the basis with `Bool` components, but SymPy can't handle that.)
const ğ¢ = Quaternion{Int}(Quaternionic.ğ¢)
const ğ£ = Quaternion{Int}(Quaternionic.ğ£)
const ğ¤ = Quaternion{Int}(Quaternionic.ğ¤)
nothing  #hide

# Next, we define functions to compute the Euler components of the left and right operators
function ğ’ª(u, side)
    ## Substitutions that sympy doesn't make but we want
    subs = Dict(
        cos(Î²)/sin(Î²) => 1/tan(Î²),
        sqrt(1 - cos(Î²))*sqrt(cos(Î²) + 1) => sin(Î²)
    )

    ## Define the essential quaternions
    e = cos(Î¸/2) + u * sin(-Î¸/2)
    Râ‚€ = Quaternion(sympy.simplify.(sympy.expand.(components(
        (cos(Î±/2) + ğ¤ * sin(Î±/2)) * (cos(Î²/2) + ğ£ * sin(Î²/2)) * (cos(Î³/2) + ğ¤ * sin(Î³/2))
    ))))

    ## Extract the (simplified) components of the product
    w, x, y, z = sympy.simplify.(sympy.expand.(components(
        side == :left ? e * Râ‚€ : Râ‚€ * e
    )))

    ## Convert back to Euler angles
    Î±â€² = (atan(z/w) + atan(-x/y)).expand().simplify()
    Î²â€² = (2*acos(sqrt(w^2 + z^2) / sqrt(w^2 + x^2 + y^2 + z^2))).expand().simplify()
    Î³â€² = (atan(z/w) - atan(-x/y)).expand().simplify()

    ## Differentiate with respect to Î¸, set Î¸ to 0, and simplify
    âˆ‚Î±â€²âˆ‚Î¸ = expand_trig(Derivative(Î±â€², Î¸).doit().subs(Î¸, 0).expand().simplify().subs(subs))
    âˆ‚Î²â€²âˆ‚Î¸ = expand_trig(Derivative(Î²â€², Î¸).doit().subs(Î¸, 0).expand().simplify().subs(subs))
    âˆ‚Î³â€²âˆ‚Î¸ = expand_trig(Derivative(Î³â€², Î¸).doit().subs(Î¸, 0).expand().simplify().subs(subs))

    return âˆ‚Î±â€²âˆ‚Î¸, âˆ‚Î²â€²âˆ‚Î¸, âˆ‚Î³â€²âˆ‚Î¸
end

## Note that we are not including the factor of ``i`` here; for simplicity, we will insert
## it manually when displaying the results below, and when applying these operators to
## functions (`Lx` and related definitions below).
L(u) = ğ’ª(u, :left)
R(u) = ğ’ª(u, :right)
nothing  #hide


# We need a couple quick helper macros to display the results.
#md # The details are boring, but you can expand the source code to see them.
#md # ```@raw html
#md # <details>
#md # <summary>
#md # Click here to expand the source code for display macros
#md # </summary>
#md # ```
macro display(expr)
    op = string(expr.args[1])
    arg = Dict(:ğ¢ => "x", :ğ£ => "y", :ğ¤ => "z")[expr.args[2]]
    if op == "L"
        quote
            âˆ‚Î±â€²âˆ‚Î¸, âˆ‚Î²â€²âˆ‚Î¸, âˆ‚Î³â€²âˆ‚Î¸ = latex.($expr)  # Call expr; format results as LaTeX
            expr = $op * "_" * $arg  # Standard form of the operator
            L"""%$expr = i\left[
                %$(âˆ‚Î±â€²âˆ‚Î¸) \frac{\partial}{\partial \alpha}
                + %$(âˆ‚Î²â€²âˆ‚Î¸) \frac{\partial}{\partial \beta}
                + %$(âˆ‚Î³â€²âˆ‚Î¸) \frac{\partial}{\partial \gamma}
            \right]"""  # Display the result in LaTeX form
        end
    else
        quote
            âˆ‚Î±â€²âˆ‚Î¸, âˆ‚Î²â€²âˆ‚Î¸, âˆ‚Î³â€²âˆ‚Î¸ = latex.($expr)  # Call expr; format results as LaTeX
            expr = $op * "_" * $arg  # Standard form of the operator
            L"""%$expr = -i\left[
                %$(âˆ‚Î±â€²âˆ‚Î¸) \frac{\partial}{\partial \alpha}
                + %$(âˆ‚Î²â€²âˆ‚Î¸) \frac{\partial}{\partial \beta}
                + %$(âˆ‚Î³â€²âˆ‚Î¸) \frac{\partial}{\partial \gamma}
            \right]"""  # Display the result in LaTeX form
        end
    end
end
nothing  #hide

# And we'll need another for the angular-momentum operators in standard ``S^2`` form.
conversion(âˆ‚) = latex(âˆ‚.subs(Dict(Î± => Ï•, Î² => Î¸, Î³ => 0)).simplify())
macro display2(expr)
    op = string(expr.args[1])
    arg = Dict(:ğ¢ => "x", :ğ£ => "y", :ğ¤ => "z")[expr.args[2]]
    if op == "L"
        quote
            âˆ‚Ï†â€²âˆ‚Î¸, âˆ‚Ï‘â€²âˆ‚Î¸, âˆ‚Î³â€²âˆ‚Î¸ = $conversion.($expr)  # Call expr; format results as LaTeX
            expr = $op * "_" * $arg  # Standard form of the operator
            L"""%$expr = i\left[
                %$(âˆ‚Ï‘â€²âˆ‚Î¸) \frac{\partial}{\partial \theta}
                + %$(âˆ‚Ï†â€²âˆ‚Î¸) \frac{\partial}{\partial \phi}
            \right]"""  # Display the result in LaTeX form
        end
    else
        quote
            âˆ‚Ï†â€²âˆ‚Î¸, âˆ‚Ï‘â€²âˆ‚Î¸, âˆ‚Î³â€²âˆ‚Î¸ = $conversion.($expr)  # Call expr; format results as LaTeX
            expr = $op * "_" * $arg  # Standard form of the operator
            L"""%$expr = -i\left[
                %$(âˆ‚Ï‘â€²âˆ‚Î¸) \frac{\partial}{\partial \theta}
                + %$(âˆ‚Ï†â€²âˆ‚Î¸) \frac{\partial}{\partial \phi}
                + %$(âˆ‚Î³â€²âˆ‚Î¸) \frac{\partial}{\partial \gamma}
            \right]"""  # Display the result in LaTeX form
        end
    end
end
nothing  #hide

#md # ```@raw html
#md # </details>
#md # ```

# ## Full expressions on ``S^3``
# Finally, we can actually compute the Euler components of the angular momentum operators.
@display L(ğ¢)
#-
@display L(ğ£)
#-
@display L(ğ¤)
#-
@display R(ğ¢)
#-
@display R(ğ£)
#-
@display R(ğ¤)

# In their description of the Wigner ğ”‡ functions as wave functions of a rigid symmetric
# top, [Varshalovich_1988](@citet) provide equivalent expressions in Eqs. (6) and (7) of
# their Sec. 4.2 â€”Â except that ``R_x`` and ``R_z`` have the wrong signs.
# [Wikipedia](https://en.wikipedia.org/wiki/Wigner_D-matrix#Properties_of_the_Wigner_D-matrix),
# meanwhile, provides equivalent expressions, except that their ``\hat{\mathcal{P}}`` has
# (consistently) the opposite sign to ``R`` defined here.

# Note that the Wikipedia convention is actually entirely sensible â€” maybe more sensible
# than the one we use.  In that convention ``\hat{J}`` is in the inertial frame, whereas
# ``\hat{P}`` is exactly that operator in the body-fixed frame.  In our notation, we have
# ```math
# \begin{align}
# R_ğ® f(ğ‘)
# &=
# -\left. i \frac{d}{d\epsilon}\right|_{\epsilon=0} f\left(ğ‘\, e^{-\epsilon ğ®/2}\right) \\
# &=
# -\left. i \frac{d}{d\epsilon}\right|_{\epsilon=0}
#   f\left(ğ‘\, e^{-\epsilon ğ®/2}\, ğ‘^{-1}\, ğ‘\right) \\
# &=
# -\left. i \frac{d}{d\epsilon}\right|_{\epsilon=0}
#   f\left(e^{-\epsilon ğ‘\, ğ®\, ğ‘^{-1}/2}\, ğ‘\right) \\
# &=
# -L_{ğ‘\, ğ®\, ğ‘^{-1}} f(ğ‘),
# \end{align}
# ```
# which says that ``R`` is just the *negative of* the ``L`` operator transformed to the
# body-fixed frame.  That negative sign is slightly unnatural, but the reason we choose to
# define ``R`` in this way is for its more natural connection to the literature on
# spin-weighted spherical functions.

# ### Commutators

# We can also compute the commutators of the angular momentum operators, as derived above.
# First, we define the operators acting on functions of the Euler angles.
f = symbols("f", cls=SymPyPythonCall.sympy.o.Function)
function ğ’ª(u, side, f, Î±, Î², Î³)
    let O = ğ’ª(u, side)
        (side==:left ? I : -I) * (
            O[1] * f(Î±, Î², Î³).diff(Î±)
            + O[2] * f(Î±, Î², Î³).diff(Î²)
            + O[3] * f(Î±, Î², Î³).diff(Î³)
        )
    end
end

Lx(f, Î±, Î², Î³) = ğ’ª(ğ¢, :left, f, Î±, Î², Î³)
Ly(f, Î±, Î², Î³) = ğ’ª(ğ£, :left, f, Î±, Î², Î³)
Lz(f, Î±, Î², Î³) = ğ’ª(ğ¤, :left, f, Î±, Î², Î³)

Rx(f, Î±, Î², Î³) = ğ’ª(ğ¢, :right, f, Î±, Î², Î³)
Ry(f, Î±, Î², Î³) = ğ’ª(ğ£, :right, f, Î±, Î², Î³)
Rz(f, Î±, Î², Î³) = ğ’ª(ğ¤, :right, f, Î±, Î², Î³)

nothing  #hide

# Now we define their commutator ``[Oâ‚, Oâ‚‚] = Oâ‚Oâ‚‚ - Oâ‚‚Oâ‚``:
function commutator(Oâ‚, Oâ‚‚)
    (
        Oâ‚((Î±, Î², Î³)->Oâ‚‚(f, Î±, Î², Î³), Î±, Î², Î³)
        - Oâ‚‚((Î±, Î², Î³)->Oâ‚(f, Î±, Î², Î³), Î±, Î², Î³)
    ).expand().simplify()
end

nothing  #hide

# And finally, evaluate each in turn.  We expect ``[L_x, L_y] = i L_z`` and cyclic
# permutations:
commutator(Lx, Ly)
#-
commutator(Ly, Lz)
#-
commutator(Lz, Lx)
# Similarly, we expect ``[R_x, R_y] = i R_z`` and cyclic permutations:
commutator(Rx, Ry)
#-
commutator(Ry, Rz)
#-
commutator(Rz, Rx)

# Just for completeness, let's evaluate the commutators of the left and right operators,
# which should all be zero.
commutator(Lx, Rx)
#-
commutator(Lx, Ry)
#-
commutator(Lx, Rz)
#-
commutator(Ly, Rx)
#-
commutator(Ly, Ry)
#-
commutator(Ly, Rz)
#-
commutator(Lz, Rx)
#-
commutator(Lz, Ry)
#-
commutator(Lz, Rz)


# ## Standard expressions on ``S^2``
# We can substitute ``(Î±, Î², Î³) \to (Ï†, Î¸, 0)`` to get the standard expressions for the
# angular momentum operators on the 2-sphere.
@display2 L(ğ¢)
#-
@display2 L(ğ£)
#-
@display2 L(ğ¤)

# Those are indeed the standard expressions for the angular-momentum operators on the
# 2-sphere, so we can declare success!
#
# Now, note that including ``\partial_\gamma`` for an expression on the 2-sphere doesn't
# actually make any sense: ``\gamma`` isn't even a coordinate for the 2-sphere!  However,
# for historical reasons, we include it here when showing the results of the ``R`` operator
# in Euler angles.

@display2 R(ğ¢)
#-
@display2 R(ğ£)
#-
@display2 R(ğ¤)

# We get nonzero components of ``\partial_\gamma``, showing that these operators really *do
# not* make sense for the 2-sphere, and therefore that it doesn't actually make sense to
# define spin-weighted spherical functions on the 2-sphere; they really only make sense on
# the 3-sphere.  Nonetheless, if we stipulate that the function ``\eta`` has a specific spin
# weight, that means that *on the 3-sphere* it is an eigenfunction with ``R_z\eta =
# i\partial_\gamma \eta = s\eta``.  So we could just substitute ``-i s`` for
# ``\partial_\gamma`` in the expressions above, and recover the standard spin-weight
# operators.  We get
# ```math
# \left[R_x + i R_y\right] \eta
# = \left[
#     -i \frac{1}{\sin\theta} \frac{\partial}{\partial \phi}
#     + \frac{s}{\tan \theta}
#     - \frac{\partial}{\partial \theta}
#   \right] \eta
# = -(\sin \theta)^s \left\{
#     \frac{\partial}{\partial \theta}
#     +i \frac{1}{\sin\theta} \frac{\partial}{\partial \phi}
#   \right\}
#   \left\{ (\sin \theta)^{-s} \eta \right\}.
# ```
# And in the latter form, we can see that ``R_x + i R_y`` is exactly the spin-raising
# operator ``\eth`` as originally defined by [Newman_1966](@citet) in their Eq. (3.8).  The
# complex-conjugate of this operator is the spin-lowering operator ``\bar{\eth}`` for
# ``R_z``.  *By definition* of raising and lowering operators, this means that ``[R_z, \eth]
# = \eth`` and ``[R_z, \bar{\eth}] = -\bar{\eth}``.  We can verify these results by
# computing the commutators directly from the expressions above:
# ```math
# \begin{aligned}
# [R_z, \eth]
#   &= [R_z, R_x] + i [R_z, R_y] = i R_y - i i R_x = R_x + i R_y = \eth,
# \\
# [R_z, \bar{\eth}]
#   &= [R_z, R_x] - i [R_z, R_y] = i R_y + i i R_x = -R_x + i R_y = -\bar{\eth}.
# \end{aligned}
# ```
#
# So we see that we've reproduced precisely the standard expressions for the spin-weighted
# spherical functions (depicted as functions on the 2-sphere) from the expressions for the
# angular-momentum operators acting on general functions on the 3-sphere.  The standard
# expressions appear arbitrarily, and are not even well defined as functions on the 2-sphere
# because they also need input from tangent space of the sphere â€” which is not part of the
# 2-sphere proper.  On the other hand, the expressions from the 3-sphere are mathematically
# and physically well defined and intuitive.  Note that the latter is complete in itself; it
# can stand alone without reference to the 2-sphere.  Rather, what we have done here is just
# shown the connection to the inadequate standard presentation.  But it is important to
# recognize that our complete treatment on ``\mathrm{Spin}(3)`` is the more fundamental one,
# and can be used without reference to the older treatment.
