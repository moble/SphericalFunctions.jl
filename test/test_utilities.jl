# NOTE: This file is for utilities used in tests ‚Äî not for tests of utilities

"""
This module is copied from [this post by Brian Guenter](
https://discourse.julialang.org/t/treating-nan-as-error-helping-debugging/36933/9).
Essentially, you can wrap your `Real` type in `NaNCheck`, and pass it (or an
array) of such objects to a function you want to check to track down where
NaNs are coming into a calculation.  As soon as such an object is used in a
calculation, it raises an exception ‚Äî essentially making this a
[signaling NaN](https://en.wikipedia.org/wiki/NaN#Signaling_NaN).

This can also be helpful for ensuring that no un-initialized memory is used in
a computation (which might silently give bad results).  Instead of creating an
`undef` array, just create the array from `NaNCheck`s.  The elements can be
set to some non-NaN value before use, and the calculation will proceed as
usual.  If they are not set to non-NaN, they will raise an exception whenever
they are used.

Such an array can be created for type `T` with something like

    arr = fill(NaNCheck{T}(NaN), arr_size)

"""
@testmodule NaNChecker begin

struct NaNCheck{T<:Real} <: Real
    val::T
    function NaNCheck{T}(a::S) where {T<:Real, S<:Real}
        @assert !(T <: NaNCheck)
        new{T}(T(a))
    end
end
export NaNCheck
Base.isnan(a::NaNCheck{T}) where{T} = isnan(a.val)
Base.isinf(a::NaNCheck{T}) where{T} = isinf(a.val)
Base.typemin(::Type{NaNCheck{T}}) where{T} = NaNCheck{T}(typemin(T))
Base.typemax(::Type{NaNCheck{T}}) where{T} = NaNCheck{T}(typemax(T))
Base.eps(::Type{NaNCheck{T}}) where {T} = NaNCheck{T}(eps(T))
Base.decompose(a::NaNCheck{T}) where {T} = Base.decompose(a.val)
Base.round(a::NaNCheck{T}, m::RoundingMode) where {T} = NaNCheck{T}(round(a.val, m))

struct NaNException <: Exception end

# (::Type{Float64})(a::NaNCheck{S}) where {S<:Real} = NaNCheck{Float64}(Float64(a.val))
(::Type{T})(a::NaNCheck{S}) where {T<:Integer,S<:Real} = T(a.val)
(::Type{NaNCheck{T}})(a::NaNCheck{S}) where {T<:Real,S<:Real} = NaNCheck{T}(T(a.val))
Base.promote_rule(::Type{NaNCheck{T}}, ::Type{T}) where {T<:Number} = NaNCheck{T}
Base.promote_rule(::Type{T}, ::Type{NaNCheck{T}}) where {T<:Number} = NaNCheck{T}
Base.promote_rule(::Type{S}, ::Type{NaNCheck{T}}) where {T<:Number, S<:Number} = NaNCheck{promote_type(T,S)}
Base.promote_rule(::Type{NaNCheck{T}}, ::Type{S}) where {T<:Number, S<:Number} = NaNCheck{promote_type(T,S)}
Base.promote_rule(::Type{NaNCheck{S}}, ::Type{NaNCheck{T}}) where {T<:Number, S<:Number} = NaNCheck{promote_type(T,S)}

for op = (:sin, :cos, :tan, :log, :exp, :sqrt, :abs, :-, :atan, :acos, :asin, :log1p, :floor, :ceil, :float)
    eval(quote
        function Base.$op(a::NaNCheck{T}) where{T}
            temp = NaNCheck{T}(Base.$op(a.val))
            if isnan(temp)
                throw(NaNException())
            end
            return temp
        end
    end)
end

for op = (:+, :-, :/, :*, :^, :atan)
    eval(quote
        function Base.$op(a::NaNCheck{T}, b::NaNCheck{T}) where{T}
            temp = NaNCheck{T}(Base.$op(a.val, b.val))
            if isnan(temp)
                throw(NaNException())
            end
            return temp
        end
    end)
end

for op =  (:<, :>, :<=, :>=, :(==), :isequal)
    eval(quote
        function Base.$op(a::NaNCheck{T}, b::NaNCheck{T}) where{T}
            temp = Base.$op(a.val, b.val)
            return temp
        end
    end)
end

end  # module NaNChecker
NaNCheck = NaNChecker.NaNCheck;


@testmodule ExplicitWignerMatrices begin

function d_explicit(n, m‚Ä≤, m, expiŒ≤::Complex{T}) where T
    if abs(m‚Ä≤) < abs(m)
        return (-1)^(m-m‚Ä≤) * d_explicit(n, m, m‚Ä≤, expiŒ≤)
    end
    if m‚Ä≤ < 0
        return (-1)^(m-m‚Ä≤) * d_explicit(n, -m‚Ä≤, -m, expiŒ≤)
    end
    cosŒ≤ = expiŒ≤.re
    sinŒ≤ = expiŒ≤.im
    if (n,m‚Ä≤,m) == (0,0,0)
        T(1)
    elseif (n,m‚Ä≤,m) == (1,0,0)
        cosŒ≤
    elseif (n,m‚Ä≤,m) == (1,1,-1)
        (1-cosŒ≤) / 2
    elseif (n,m‚Ä≤,m) == (1,1,0)
        -sinŒ≤ / ‚àöT(2)
    elseif (n,m‚Ä≤,m) == (1,1,1)
        (1+cosŒ≤) / 2
    elseif (n,m‚Ä≤,m) == (2,0,0)
        (3cosŒ≤^2-1) / 2
    elseif (n,m‚Ä≤,m) == (2,1,-1)
        (1+cosŒ≤-2cosŒ≤^2) / 2
    elseif (n,m‚Ä≤,m) == (2,1,0)
        -‚àö(T(3)/8) * 2 * sinŒ≤ * cosŒ≤
    elseif (n,m‚Ä≤,m) == (2,1,1)
        (-1+cosŒ≤+2cosŒ≤^2) / 2
    elseif (n,m‚Ä≤,m) == (2,2,-2)
        (1-cosŒ≤)^2 / 4
    elseif (n,m‚Ä≤,m) == (2,2,-1)
        -sinŒ≤ * (1-cosŒ≤) / 2
    elseif (n,m‚Ä≤,m) == (2,2,0)
        ‚àö(T(3)/8) * sinŒ≤^2
    elseif (n,m‚Ä≤,m) == (2,2,1)
        -sinŒ≤ * (1+cosŒ≤) / 2
    elseif (n,m‚Ä≤,m) == (2,2,2)
        (1+cosŒ≤)^2/4
    else
        T(NaN)
    end
end

function D_explicit(n, m‚Ä≤, m, expiŒ±::Complex{T}, expiŒ≤::Complex{T}, expiŒ≥::Complex{T}) where T
    return expiŒ±^(-m‚Ä≤) * d_explicit(n, m‚Ä≤, m, expiŒ≤) * expiŒ≥^(-m)
end


function d_formula(n, m‚Ä≤, m, expiŒ≤::Complex{T}) where T
    # https://en.wikipedia.org/wiki/Wigner_D-matrix#Wigner_.28small.29_d-matrix
    cosŒ≤ = expiŒ≤.re
    sin¬ΩŒ≤ = ‚àö((1-cosŒ≤)/2)
    cos¬ΩŒ≤ = ‚àö((1+cosŒ≤)/2)
    prefactor =‚àöT(
        factorial(big(n + m‚Ä≤))
        * factorial(big(n - m‚Ä≤))
        * factorial(big(n + m))
        * factorial(big(n - m))
    )
    prefactor * sum(
        ifelse(iseven(m‚Ä≤ - m + s), 1, -1)
        * cos¬ΩŒ≤ ^ (2n + m - m‚Ä≤ - 2s)
        * sin¬ΩŒ≤ ^ (m‚Ä≤ - m + 2s)
        / (
            factorial(big(n + m - s))
            * factorial(big(s))
            * factorial(big(m‚Ä≤ - m + s))
            * factorial(big(n - m‚Ä≤ - s))
        )
        for s in max(0, m - m‚Ä≤):min(n + m, n - m‚Ä≤)
    )
end

function D_formula(n, m‚Ä≤, m, expiŒ±::Complex{T}, expiŒ≤::Complex{T}, expiŒ≥::Complex{T}) where T
    # https://en.wikipedia.org/wiki/Wigner_D-matrix#Definition_of_the_Wigner_D-matrix
    # Note that the convention in this package is conjugated relative to the convention
    # used by Wikipedia, so we include that conjugation here.
    return expiŒ±^(m‚Ä≤) * d_formula(n, m‚Ä≤, m, expiŒ≤) * expiŒ≥^(m)
end

end  # module ExplicitWignerMatrices


@testmodule NINJA begin

function Wigner_d(Œπ::T, ell, m, s) where {T<:Real}
    # Eq. II.8 of Ajith et al. (2007) 'Data formats...'
    k_min = max(0, m - s)
    k_max = min(ell + m, ell - s)
    prefactor = ‚àöT(
        factorial(big(ell + m))
        * factorial(big(ell - m))
        * factorial(big(ell + s))
        * factorial(big(ell - s))
    )
    sum(
        ifelse(iseven(k), 1, -1)
         * cos(Œπ / 2) ^ (2 * ell + m - s - 2 * k)
         * sin(Œπ / 2) ^ (2 * k + s - m)
         * prefactor
         / T(
             factorial(big(ell + m - k))
             * factorial(big(ell - s - k))
             * factorial(big(k))
             * factorial(big(k + s - m))
         )
        for k in k_min:k_max
    )
end

raw"""

Eq. II.7 of Ajith et al. (2007) 'Data formats...' says
```math
{}_sY_{\ell,m} = (-1)^s \sqrt{\frac{2\ell+1}{4\pi}} d^\ell_{m,-s}(\iota) e^{im\phi}
```

Below Eq. (2.53) of [Torres del Castillo](@cite TorresDelCastillo_2003), we see
```math
{}_sY_{j,m} = (-1)^m \sqrt{\frac{2j+1}{4\pi}} d^j_{-m,s}(\iota) e^{im\phi}
```
We can use identities to modify the latter as follows:
```math
\begin{aligned}
{}_sY_{j,m} &= (-1)^m \sqrt{\frac{2j+1}{4\pi}} d^j_{-m,s}(\iota) e^{im\phi} \\
           &= (-1)^m \sqrt{\frac{2j+1}{4\pi}} d^j_{-s,m}(\iota) e^{im\phi} \\
           &= (-1)^{j-s+m} \sqrt{\frac{2j+1}{4\pi}} d^j_{-s,-m}(\pi-\iota) e^{im\phi} \\
           &= (-1)^{j-s+m} \sqrt{\frac{2j+1}{4\pi}} d^j_{m,s}(\pi-\iota) e^{im\phi} \\
           &= (-1)^{2j-s+2m} \sqrt{\frac{2j+1}{4\pi}} d^j_{m,-s}(\pi-(\pi-\iota)) e^{im\phi} \\
           &= (-1)^{s} \sqrt{\frac{2j+1}{4\pi}} d^j_{m,-s}(\iota) e^{im\phi} \\
\end{aligned}
```
The last line assumes that `j`, `m`, and `s` are integers.  But in that case, the NINJA
expression agrees with the Torres del Castillo expression.


"""

function sYlm(s, ell, m, Œπ::T, œï::T) where {T<:Real}
    # Eq. II.7 of Ajith et al. (2007) 'Data formats...'
    # Note the weird definition w.r.t. `-s`
    if abs(s) > ell || abs(m) > ell
        return zero(complex(T))
    end
    (
        ifelse(iseven(s), 1, -1)
        * ‚àö((2ell + 1) / (4T(œÄ)))
        * Wigner_d(Œπ, ell, m, -s)
        * cis(m * œï)
    )
end

function sYlm(s, ‚Ñì, m, Œπœï)
    sYlm(s, ‚Ñì, m, Œπœï[1], Œπœï[2])
end

# Eqs. (II.9) through (II.13) of https://arxiv.org/abs/0709.0093v3 [Ajith_2007](@cite)
m2Y22(Œπ::T, œï::T) where {T<:Real} = ‚àö(5 / (64T(œÄ))) * (1 + cos(Œπ))^2 * cis(2œï)
m2Y21(Œπ::T, œï::T) where {T<:Real} = ‚àö(5 / (16T(œÄ))) * sin(Œπ) * (1 + cos(Œπ)) * cis(œï)
m2Y20(Œπ::T, œï::T) where {T<:Real} = ‚àö(15 / (32T(œÄ))) * sin(Œπ)^2
m2Y2m1(Œπ::T, œï::T) where {T<:Real} = ‚àö(5 / (16T(œÄ))) * sin(Œπ) * (1 - cos(Œπ)) * cis(-1œï)
m2Y2m2(Œπ::T, œï::T) where {T<:Real} = ‚àö(5 / (64T(œÄ))) * (1 - cos(Œπ))^2 * cis(-2œï)

m_m2Y2m = [
    (2, m2Y22),
    (1, m2Y21),
    (0, m2Y20),
    (-1, m2Y2m1),
    (-2, m2Y2m2)
]

end  # module NINJA


@testmodule LAL begin

"""
Reproduces the XLALSpinWeightedSphericalHarmonic function from the LALSuite C library:
    https://lscsoft.docs.ligo.org/lalsuite/lal/_spherical_harmonics_8c_source.html#l00042
"""
function LALSpinWeightedSphericalHarmonic(
    theta::Float64,  # polar angle (rad)
    phi::Float64,    # azimuthal angle (rad)
    s::Int,   # spin weight
    l::Int,   # mode number l
    m::Int    # mode number m
)
    # Sanity checks
    if l < abs(s)
        error("Invalid mode s=$s, l=$l, m=$m - require |s| <= l")
    end
    if l < abs(m)
        error("Invalid mode s=$s, l=$l, m=$m - require |m| <= l")
    end
    if s != -2
        error("Unsupported mode s=$s (only s=-2 implemented)")
    end
    if l < 2 || l > 8
        error("Unsupported mode l=$l (only l in [2,8] implemented)")
    end
    # Compute real factor
    fac = if l == 2
        if m == -2
            sqrt(5.0 / (64.0 * œÄ)) * (1.0 - cos(theta)) * (1.0 - cos(theta))
        elseif m == -1
            sqrt(5.0 / (16.0 * œÄ)) * sin(theta) * (1.0 - cos(theta))
        elseif m == 0
            sqrt(15.0 / (32.0 * œÄ)) * sin(theta) * sin(theta)
        elseif m == 1
            sqrt(5.0 / (16.0 * œÄ)) * sin(theta) * (1.0 + cos(theta))
        elseif m == 2
            sqrt(5.0 / (64.0 * œÄ)) * (1.0 + cos(theta)) * (1.0 + cos(theta))
        end
    elseif l == 3
        if m == -3
            sqrt(21.0 / (2.0 * œÄ)) * cos(theta/2.0) * sin(theta/2.0)^5
        elseif m == -2
            sqrt(7.0 / (4.0 * œÄ)) * (2.0 + 3.0 * cos(theta)) * sin(theta/2.0)^4
        elseif m == -1
            sqrt(35.0 / (2.0 * œÄ)) * (sin(theta) + 4.0 * sin(2.0 * theta) - 3.0 * sin(3.0 * theta)) / 32.0
        elseif m == 0
            sqrt(105.0 / (2.0 * œÄ)) * cos(theta) * sin(theta)^2 / 4.0
        elseif m == 1
            -sqrt(35.0 / (2.0 * œÄ)) * (sin(theta) - 4.0 * sin(2.0 * theta) - 3.0 * sin(3.0 * theta)) / 32.0
        elseif m == 2
            sqrt(7.0 / œÄ) * cos(theta/2.0)^4 * (-2.0 + 3.0 * cos(theta)) / 2.0
        elseif m == 3
            -sqrt(21.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * sin(theta/2.0)
        end
    elseif l == 4
        if m == -4
            3.0 * sqrt(7.0 / œÄ) * cos(theta/2.0)^2 * sin(theta/2.0)^6
        elseif m == -3
            3.0 * sqrt(7.0 / (2.0 * œÄ)) * cos(theta/2.0) * (1.0 + 2.0 * cos(theta)) * sin(theta/2.0)^5
        elseif m == -2
            3.0 * (9.0 + 14.0 * cos(theta) + 7.0 * cos(2.0 * theta)) * sin(theta/2.0)^4 / (4.0 * sqrt(œÄ))
        elseif m == -1
            3.0 * (3.0 * sin(theta) + 2.0 * sin(2.0 * theta) + 7.0 * sin(3.0 * theta) - 7.0 * sin(4.0 * theta)) / (32.0 * sqrt(2.0 * œÄ))
        elseif m == 0
            3.0 * sqrt(5.0 / (2.0 * œÄ)) * (5.0 + 7.0 * cos(2.0 * theta)) * sin(theta)^2 / 16.0
        elseif m == 1
            3.0 * (3.0 * sin(theta) - 2.0 * sin(2.0 * theta) + 7.0 * sin(3.0 * theta) + 7.0 * sin(4.0 * theta)) / (32.0 * sqrt(2.0 * œÄ))
        elseif m == 2
            3.0 * cos(theta/2.0)^4 * (9.0 - 14.0 * cos(theta) + 7.0 * cos(2.0 * theta)) / (4.0 * sqrt(œÄ))
        elseif m == 3
            -3.0 * sqrt(7.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * (-1.0 + 2.0 * cos(theta)) * sin(theta/2.0)
        elseif m == 4
            3.0 * sqrt(7.0 / œÄ) * cos(theta/2.0)^6 * sin(theta/2.0)^2
        end
    elseif l == 5
        if m == -5
            sqrt(330.0 / œÄ) * cos(theta/2.0)^3 * sin(theta/2.0)^7
        elseif m == -4
            sqrt(33.0 / œÄ) * cos(theta/2.0)^2 * (2.0 + 5.0 * cos(theta)) * sin(theta/2.0)^6
        elseif m == -3
            sqrt(33.0 / (2.0 * œÄ)) * cos(theta/2.0) * (17.0 + 24.0 * cos(theta) + 15.0 * cos(2.0 * theta)) * sin(theta/2.0)^5 / 4.0
        elseif m == -2
            sqrt(11.0 / œÄ) * (32.0 + 57.0 * cos(theta) + 36.0 * cos(2.0 * theta) + 15.0 * cos(3.0 * theta)) * sin(theta/2.0)^4 / 8.0
        elseif m == -1
            sqrt(77.0 / œÄ) * (2.0 * sin(theta) + 8.0 * sin(2.0 * theta) + 3.0 * sin(3.0 * theta) + 12.0 * sin(4.0 * theta) - 15.0 * sin(5.0 * theta)) / 256.0
        elseif m == 0
            sqrt(1155.0 / (2.0 * œÄ)) * (5.0 * cos(theta) + 3.0 * cos(3.0 * theta)) * sin(theta)^2 / 32.0
        elseif m == 1
            sqrt(77.0 / œÄ) * (-2.0 * sin(theta) + 8.0 * sin(2.0 * theta) - 3.0 * sin(3.0 * theta) + 12.0 * sin(4.0 * theta) + 15.0 * sin(5.0 * theta)) / 256.0
        elseif m == 2
            sqrt(11.0 / œÄ) * cos(theta/2.0)^4 * (-32.0 + 57.0 * cos(theta) - 36.0 * cos(2.0 * theta) + 15.0 * cos(3.0 * theta)) / 8.0
        elseif m == 3
            -sqrt(33.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * (17.0 - 24.0 * cos(theta) + 15.0 * cos(2.0 * theta)) * sin(theta/2.0) / 4.0
        elseif m == 4
            sqrt(33.0 / œÄ) * cos(theta/2.0)^6 * (-2.0 + 5.0 * cos(theta)) * sin(theta/2.0)^2
        elseif m == 5
            -sqrt(330.0 / œÄ) * cos(theta/2.0)^7 * sin(theta/2.0)^3
        end
    elseif l == 6
        if m == -6
            (3.0 * sqrt(715.0 / œÄ) * cos(theta/2.0)^4 * sin(theta/2.0)^8) / 2.0
        elseif m == -5
            (sqrt(2145.0 / œÄ) * cos(theta/2.0)^3 * (1.0 + 3.0 * cos(theta)) * sin(theta/2.0)^7) / 2.0
        elseif m == -4
            (sqrt(195.0 / (2.0 * œÄ)) * cos(theta/2.0)^2 * (35.0 + 44.0 * cos(theta) + 33.0 * cos(2.0 * theta)) * sin(theta/2.0)^6) / 8.0
        elseif m == -3
            (3.0 * sqrt(13.0 / œÄ) * cos(theta/2.0) * (98.0 + 185.0 * cos(theta) + 110.0 * cos(2.0 * theta) + 55.0 * cos(3.0 * theta)) * sin(theta/2.0)^5) / 32.0
        elseif m == -2
            (sqrt(13.0 / œÄ) * (1709.0 + 3096.0 * cos(theta) + 2340.0 * cos(2.0 * theta) + 1320.0 * cos(3.0 * theta) + 495.0 * cos(4.0 * theta)) * sin(theta/2.0)^4) / 256.0
        elseif m == -1
            (sqrt(65.0 / (2.0 * œÄ)) * cos(theta/2.0) * (161.0 + 252.0 * cos(theta) + 252.0 * cos(2.0 * theta) + 132.0 * cos(3.0 * theta) + 99.0 * cos(4.0 * theta)) * sin(theta/2.0)^3) / 64.0
        elseif m == 0
            (sqrt(1365.0 / œÄ) * (35.0 + 60.0 * cos(2.0 * theta) + 33.0 * cos(4.0 * theta)) * sin(theta)^2) / 512.0
        elseif m == 1
            (sqrt(65.0 / (2.0 * œÄ)) * cos(theta/2.0)^3 * (161.0 - 252.0 * cos(theta) + 252.0 * cos(2.0 * theta) - 132.0 * cos(3.0 * theta) + 99.0 * cos(4.0 * theta)) * sin(theta/2.0)) / 64.0
        elseif m == 2
            (sqrt(13.0 / œÄ) * cos(theta/2.0)^4 * (1709.0 - 3096.0 * cos(theta) + 2340.0 * cos(2.0 * theta) - 1320.0 * cos(3.0 * theta) + 495.0 * cos(4.0 * theta))) / 256.0
        elseif m == 3
            (-3.0 * sqrt(13.0 / œÄ) * cos(theta/2.0)^5 * (-98.0 + 185.0 * cos(theta) - 110.0 * cos(2.0 * theta) + 55.0 * cos(3.0 * theta)) * sin(theta/2.0)) / 32.0
        elseif m == 4
            (sqrt(195.0 / (2.0 * œÄ)) * cos(theta/2.0)^6 * (35.0 - 44.0 * cos(theta) + 33.0 * cos(2.0 * theta)) * sin(theta/2.0)^2) / 8.0
        elseif m == 5
            (-sqrt(2145.0 / œÄ) * cos(theta/2.0)^7 * (-1.0 + 3.0 * cos(theta)) * sin(theta/2.0)^3) / 2.0
        elseif m == 6
            (3.0 * sqrt(715.0 / œÄ) * cos(theta/2.0)^8 * sin(theta/2.0)^4) / 2.0
        end
    elseif l == 7
        if m == -7
            sqrt(15015.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * sin(theta/2.0)^9
        elseif m == -6
            (sqrt(2145.0 / œÄ) * cos(theta/2.0)^4 * (2.0 + 7.0 * cos(theta)) * sin(theta/2.0)^8) / 2.0
        elseif m == -5
            (sqrt(165.0 / (2.0 * œÄ)) * cos(theta/2.0)^3 * (93.0 + 104.0 * cos(theta) + 91.0 * cos(2.0 * theta)) * sin(theta/2.0)^7) / 8.0
        elseif m == -4
            (sqrt(165.0 / (2.0 * œÄ)) * cos(theta/2.0)^2 * (140.0 + 285.0 * cos(theta) + 156.0 * cos(2.0 * theta) + 91.0 * cos(3.0 * theta)) * sin(theta/2.0)^6) / 16.0
        elseif m == -3
            (sqrt(15.0 / (2.0 * œÄ)) * cos(theta/2.0) * (3115.0 + 5456.0 * cos(theta) + 4268.0 * cos(2.0 * theta) + 2288.0 * cos(3.0 * theta) + 1001.0 * cos(4.0 * theta)) * sin(theta/2.0)^5) / 128.0
        elseif m == -2
            (sqrt(15.0 / œÄ) * (5220.0 + 9810.0 * cos(theta) + 7920.0 * cos(2.0 * theta) + 5445.0 * cos(3.0 * theta) + 2860.0 * cos(4.0 * theta) + 1001.0 * cos(5.0 * theta)) * sin(theta/2.0)^4) / 512.0
        elseif m == -1
            (3.0 * sqrt(5.0 / (2.0 * œÄ)) * cos(theta/2.0) * (1890.0 + 4130.0 * cos(theta) + 3080.0 * cos(2.0 * theta) + 2805.0 * cos(3.0 * theta) + 1430.0 * cos(4.0 * theta) + 1001.0 * cos(5.0 * theta)) * sin(theta/2.0)^3) / 512.0
        elseif m == 0
            (3.0 * sqrt(35.0 / œÄ) * cos(theta) * (109.0 + 132.0 * cos(2.0 * theta) + 143.0 * cos(4.0 * theta)) * sin(theta)^2) / 512.0
        elseif m == 1
            (3.0 * sqrt(5.0 / (2.0 * œÄ)) * cos(theta/2.0)^3 * (-1890.0 + 4130.0 * cos(theta) - 3080.0 * cos(2.0 * theta) + 2805.0 * cos(3.0 * theta) - 1430.0 * cos(4.0 * theta) + 1001.0 * cos(5.0 * theta)) * sin(theta/2.0)) / 512.0
        elseif m == 2
            (sqrt(15.0 / œÄ) * cos(theta/2.0)^4 * (-5220.0 + 9810.0 * cos(theta) - 7920.0 * cos(2.0 * theta) + 5445.0 * cos(3.0 * theta) - 2860.0 * cos(4.0 * theta) + 1001.0 * cos(5.0 * theta))) / 512.0
        elseif m == 3
            -(sqrt(15.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * (3115.0 - 5456.0 * cos(theta) + 4268.0 * cos(2.0 * theta) - 2288.0 * cos(3.0 * theta) + 1001.0 * cos(4.0 * theta)) * sin(theta/2.0)) / 128.0
        elseif m == 4
            (sqrt(165.0 / (2.0 * œÄ)) * cos(theta/2.0)^6 * (-140.0 + 285.0 * cos(theta) - 156.0 * cos(2.0 * theta) + 91.0 * cos(3.0 * theta)) * sin(theta/2.0)^2) / 16.0
        elseif m == 5
            -(sqrt(165.0 / (2.0 * œÄ)) * cos(theta/2.0)^7 * (93.0 - 104.0 * cos(theta) + 91.0 * cos(2.0 * theta)) * sin(theta/2.0)^3) / 8.0
        elseif m == 6
            (sqrt(2145.0 / œÄ) * cos(theta/2.0)^8 * (-2.0 + 7.0 * cos(theta)) * sin(theta/2.0)^4) / 2.0
        elseif m == 7
            -(sqrt(15015.0 / (2.0 * œÄ)) * cos(theta/2.0)^9 * sin(theta/2.0)^5)
        end
    elseif l == 8
        if m == -8
            sqrt(34034.0 / œÄ) * cos(theta/2.0)^6 * sin(theta/2.0)^10
        elseif m == -7
            sqrt(17017.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * (1.0 + 4.0 * cos(theta)) * sin(theta/2.0)^9
        elseif m == -6
            sqrt(255255.0 / œÄ) * cos(theta/2.0)^4 * (1.0 + 2.0 * cos(theta)) * sin(œÄ/4.0 - theta/2.0) * sin(œÄ/4.0 + theta/2.0) * sin(theta/2.0)^8
        elseif m == -5
            (sqrt(12155.0 / (2.0 * œÄ)) * cos(theta/2.0)^3 * (19.0 + 42.0 * cos(theta) + 21.0 * cos(2.0 * theta) + 14.0 * cos(3.0 * theta)) * sin(theta/2.0)^7) / 8.0
        elseif m == -4
            (sqrt(935.0 / (2.0 * œÄ)) * cos(theta/2.0)^2 * (265.0 + 442.0 * cos(theta) + 364.0 * cos(2.0 * theta) + 182.0 * cos(3.0 * theta) + 91.0 * cos(4.0 * theta)) * sin(theta/2.0)^6) / 32.0
        elseif m == -3
            (sqrt(561.0 / (2.0 * œÄ)) * cos(theta/2.0) * (869.0 + 1660.0 * cos(theta) + 1300.0 * cos(2.0 * theta) + 910.0 * cos(3.0 * theta) + 455.0 * cos(4.0 * theta) + 182.0 * cos(5.0 * theta)) * sin(theta/2.0)^5) / 128.0
        elseif m == -2
            (sqrt(17.0 / œÄ) * (7626.0 + 14454.0 * cos(theta) + 12375.0 * cos(2.0 * theta) + 9295.0 * cos(3.0 * theta) + 6006.0 * cos(4.0 * theta) + 3003.0 * cos(5.0 * theta) + 1001.0 * cos(6.0 * theta)) * sin(theta/2.0)^4) / 512.0
        elseif m == -1
            (sqrt(595.0 / (2.0 * œÄ)) * cos(theta/2.0) * (798.0 + 1386.0 * cos(theta) + 1386.0 * cos(2.0 * theta) + 1001.0 * cos(3.0 * theta) + 858.0 * cos(4.0 * theta) + 429.0 * cos(5.0 * theta) + 286.0 * cos(6.0 * theta)) * sin(theta/2.0)^3) / 512.0
        elseif m == 0
            (3.0 * sqrt(595.0 / œÄ) * (210.0 + 385.0 * cos(2.0 * theta) + 286.0 * cos(4.0 * theta) + 143.0 * cos(6.0 * theta)) * sin(theta)^2) / 4096.0
        elseif m == 1
            (sqrt(595.0 / (2.0 * œÄ)) * cos(theta/2.0)^3 * (798.0 - 1386.0 * cos(theta) + 1386.0 * cos(2.0 * theta) - 1001.0 * cos(3.0 * theta) + 858.0 * cos(4.0 * theta) - 429.0 * cos(5.0 * theta) + 286.0 * cos(6.0 * theta)) * sin(theta/2.0)) / 512.0
        elseif m == 2
            (sqrt(17.0 / œÄ) * cos(theta/2.0)^4 * (7626.0 - 14454.0 * cos(theta) + 12375.0 * cos(2.0 * theta) - 9295.0 * cos(3.0 * theta) + 6006.0 * cos(4.0 * theta) - 3003.0 * cos(5.0 * theta) + 1001.0 * cos(6.0 * theta))) / 512.0
        elseif m == 3
            -(sqrt(561.0 / (2.0 * œÄ)) * cos(theta/2.0)^5 * (-869.0 + 1660.0 * cos(theta) - 1300.0 * cos(2.0 * theta) + 910.0 * cos(3.0 * theta) - 455.0 * cos(4.0 * theta) + 182.0 * cos(5.0 * theta)) * sin(theta/2.0)) / 128.0
        elseif m == 4
            (sqrt(935.0 / (2.0 * œÄ)) * cos(theta/2.0)^6 * (265.0 - 442.0 * cos(theta) + 364.0 * cos(2.0 * theta) - 182.0 * cos(3.0 * theta) + 91.0 * cos(4.0 * theta)) * sin(theta/2.0)^2) / 32.0
        elseif m == 5
            -(sqrt(12155.0 / (2.0 * œÄ)) * cos(theta/2.0)^7 * (-19.0 + 42.0 * cos(theta) - 21.0 * cos(2.0 * theta) + 14.0 * cos(3.0 * theta)) * sin(theta/2.0)^3) / 8.0
        elseif m == 6
            (sqrt(255255.0 / œÄ) * cos(theta/2.0)^8 * (-1.0 + 2.0 * cos(theta)) * sin(theta/2.0)^4) * sin(œÄ/4.0 - theta/2.0) * sin(œÄ/4.0 + theta/2.0);
        elseif m == 7
            -(sqrt(17017.0 / (2.0 * œÄ)) * cos(theta/2.0)^9 * (-1.0 + 4.0 * cos(theta)) * sin(theta/2.0)^5)
        elseif m == 8
            sqrt(34034.0 / œÄ) * cos(theta/2.0)^10 * sin(theta/2.0)^6
        end
    end
    # Include complex phase factor
    if m ‚â† 0
        ans = cis(m*phi) * fac
    else
        ans = fac
    end
end

end  # module LAL


@testsnippet Utilities begin

Œ±range(::Type{T}, n=15) where T = T[
    0; nextfloat(T(0)); rand(T(0):eps(T(œÄ)):T(œÄ), n√∑2); prevfloat(T(œÄ)); T(œÄ);
    nextfloat(T(œÄ)); rand(T(œÄ):eps(2T(œÄ)):2T(œÄ), n√∑2); prevfloat(T(œÄ)); 2T(œÄ)
]
Œ≤range(::Type{T}, n=15) where T = T[
    0; nextfloat(T(0)); rand(T(0):eps(T(œÄ)):T(œÄ), n); prevfloat(T(œÄ)); T(œÄ)
]
Œ≥range(::Type{T}, n=15) where T = Œ±range(T, n)
vÃÇrange(::Type{T}, n=15) where T = QuatVec{T}[
    ùê¢; ùê£; ùê§;
    -ùê¢; -ùê£; -ùê§;
    Quaternionic.normalize.(randn(QuatVec{T}, n))
]
function Rrange(::Type{T}, n=15) where T
    invsqrt2 = inv(‚àöT(2))
    [
        [
            sign*R
            for R in [
                Rotor{T}(1);
                [Rotor{T}(ùêØ) for ùêØ in (ùê¢,ùê£,ùê§)];
                [Rotor{T}(invsqrt2 + invsqrt2*ùêØ) for ùêØ in (ùê¢,ùê£,ùê§)];
                [Rotor{T}(invsqrt2 - invsqrt2*ùêØ) for ùêØ in (ùê¢,ùê£,ùê§)]
        ]
        for sign in (1,-1)
        ];
        randn(Rotor{T}, n)
    ]
end
epsilon(k) = ifelse(k>0 && isodd(k), -1, 1)

"""
    array_equal(a1, a2, equal_nan=false)

Ensure that arrays have same types and shapes, and all elements are the same.
If `equal_nan` is `true`, NaNs in the same place in each array will be
considered to be equal.

Note that this is slightly stricter than the numpy version of this function,
because arrays of different type will not be considered equal.

"""
function array_equal(a1::T1, a2::T2, equal_nan=false) where {T1, T2}
    if T1 !== T2 || size(a1) != size(a2)
        return false
    end
    all(e->e[1]==e[2] || (equal_nan && isnan(e1) && isnan(e2)), zip(a1, a2))
end

function sYlm(s::Int, ell::Int, m::Int, theta::T, phi::T) where {T<:Real}
    # Eqs. (II.7) and (II.8) of https://arxiv.org/abs/0709.0093v3 [Ajith_2007](@cite)
    # Note their weird definition w.r.t. `-s`
    k_min = max(0, m + s)
    k_max = min(ell + m, ell + s)
    sin_half_theta, cos_half_theta = sincos(theta / 2)
    return (-1)^(-s) * sqrt((2 * ell + 1) / (4 * T(œÄ))) *
        T(sum(
            (-1) ^ (k)
            * sqrt(factorial(big(ell + m)) * factorial(big(ell - m)) * factorial(big(ell - s)) * factorial(big(ell + s)))
            * (cos_half_theta ^ (2 * ell + m + s - 2 * k))
            * (sin_half_theta ^ (2 * k - s - m))
            / (factorial(big(ell + m - k)) * factorial(big(ell + s - k)) * factorial(big(k)) * factorial(big(k - s - m)))
            for k in k_min:k_max
        )) *
        cis(m * phi)
end

Œµ(j,k,l) = ifelse(
    (j,k,l)‚àà((1,2,3),(2,3,1),(3,1,2)),
    1,
    ifelse(
        (j,k,l)‚àà((2,1,3),(1,3,2),(3,2,1)),
        -1,
        0
    )
)

end  # Utilities snippet


@testmodule ExplicitOperators begin
    using Quaternionic
    import ForwardDiff

    # These are just simple versions of the operators defined in
    # notes/operators/explicit_definition.jl, for testing purposes.  Note that we explicitly
    # use `cos(Œ∏) + sin(Œ∏)*g` instead of simply `exp(Œ∏*g)`, because the `exp` implementation
    # currently has a special case at zero, which messes with the derivative at that point.
    # But also note that these are incorrect for `g=0` because we oversimplify.
    function L(g::QuatVec{T}, f) where T
        function L_g(Q)
            -im * ForwardDiff.derivative(Œ∏ -> f((cos(Œ∏) + sin(Œ∏)*g) * Q), zero(T)) / 2
        end
    end
    function R(g::QuatVec{T}, f) where T
        function R_g(Q)
            -im * ForwardDiff.derivative(Œ∏ -> f(Q * (cos(Œ∏) + sin(Œ∏)*g)), zero(T)) / 2
        end
    end

end


"""Write factorials in the obvious way, for testing purposes.

This snippet lets us write things like `5‚ùó` to get `120`, for example.  This should
probably only be used in tests, because factorials are not very efficient.  Therefore,
to ensure accuracy and avoid overflow, the argument is first converted to a `BigInt`,
making it even more inefficient.  This "big" conversion should be preserved as long as
possible to ensure accurate cancellations in the final result.
"""
@testsnippet NaiveFactorials begin
    struct Factorial end
    Base.:*(n::Integer, ::Factorial) = factorial(big(n))
    const ‚ùó = Factorial()
end
